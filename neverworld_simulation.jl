# Neverworld
#
# Ingredients:
#
#   * Zonally-periodic domain with continental shelves on all boundaries except Southern Ocean
#       * longitude = (0, 60)
#       * 2 configurations in latitude
#           - Half basin: latitude = (-70, 0)
#           - Full basin: latitude = (-70, 70)
#       * z to -4000m
#       * Southern Ocean channel from -60 to -40 (with a ridge to -2000 m)
#
#   * Zonally-homogeneous wind stress with mid-latitude jet and trade winds
#
#   * Buoyancy 
#       * restoring hot at the equator and cold at the poles (parabola, cosine, smooth step function)
#       * equator-pole buoyancy differential: 0.06 (Î± * g * 30 â‰ˆ 0.06 with Î±=2e-4, g=9.81)
#       * exponential initial vertical stratification with NÂ² = 6e-5 and decay scale h = 1000 m
#           - eg báµ¢ = NÂ² * h * exp(z / h)
#
#   * Quadratic bottom drag with drag_coefficient = 1e-3

using CubicSplines

using Oceananigans
using Oceananigans.Units
using Oceananigans.Grids: Î»node, Ï†node, minimum_xspacing, minimum_yspacing
using Oceananigans.ImmersedBoundaries: PartialCellBottom
using Oceananigans.Operators: xspacing, yspacing
using Oceananigans.Operators: Î”zá¶œá¶œá¶œ
using Oceananigans.Coriolis: ActiveCellEnstrophyConserving
using Oceananigans.TurbulenceClosures: CATKEVerticalDiffusivity

using ClimaOcean: u_bottom_drag, u_immersed_bottom_drag,
                  v_bottom_drag, v_immersed_bottom_drag

using ClimaOcean.VerticalGrids: stretched_vertical_faces, PowerLawStretching

const c = Center()
const f = Face()


#####
##### CubicSpline
#####

struct CubicSplineFunction{d, FT, S} <: Function
    coordinates :: Vector{FT}
    nodes :: Vector{FT}
    spline :: S

    @doc """
        CubicSplineFunction{d}(coordinates, nodes, FT=Float64)
    Return a function-like object that interpolates `nodes` between the `coordinates`
    in dimension `d` using cubic splines. `d` can be either `x`, `y`, or `z`.
    """
    function CubicSplineFunction{d}(coordinates, nodes, FT=Float64) where d
        # Hack to enforce no-gradient boundary conditions,
        # since CubicSplines doesn't support natively
        Î”L = coordinates[2] - coordinates[1]
        pushfirst!(coordinates, coordinates[1] - Î”L)

        Î”R = coordinates[end] - coordinates[end-1]
        push!(coordinates, coordinates[end] + Î”R)

        pushfirst!(nodes, nodes[1])
        push!(nodes, nodes[end])

        coordinates = Vector{FT}(coordinates)
        nodes = Vector{FT}(nodes)

        # Now we can build the spline
        spline = CubicSpline(coordinates, nodes)
        S = typeof(spline)

        d == :x || d == :y || d == :z || error("Dimension 'd' must be :x or :y or :z")

        return new{d, FT, S}(coordinates, nodes, spline)
    end
end

(csf::CubicSplineFunction{:x})(x, y=nothing, z=nothing) = csf.spline[x]
(csf::CubicSplineFunction{:y})(x, y, z=nothing)         = csf.spline[y]
(csf::CubicSplineFunction{:y})(y)                       = csf.spline[y]
(csf::CubicSplineFunction{:z})(x, y, z)                 = csf.spline[z]
(csf::CubicSplineFunction{:z})(z)                       = csf.spline[z]


#####
##### Utility
#####

instantiate(T::DataType) = T()
instantiate(t) = t

#####
##### Geometry
#####

struct Point{T}
    x :: T
    y :: T
end

struct LineSegment{T}
    pâ‚ :: Point{T}
    pâ‚‚ :: Point{T}
end

struct Line{T}
    pâ‚ :: Point{T}
    pâ‚‚ :: Point{T}
end

distance(pâ‚::Point, pâ‚‚::Point) = sqrt((pâ‚.x - pâ‚‚.x)^2 + (pâ‚.y - pâ‚‚.y)^2)

"""
   distance(point::Point, linesegment::LineSegment)
Return the distance between a `point` and a `linesegment`, that is the shortest distance
 of the `point` to any of the points within the line segment.
"""
function distance(point::Point, linesegment::LineSegment)
    x, y = point.x, point.y
    xâ‚, yâ‚ = linesegment.pâ‚.x, linesegment.pâ‚.y
    xâ‚‚, yâ‚‚ = linesegment.pâ‚‚.x, linesegment.pâ‚‚.y

    # Line segment vector components
    â„“Ë£ = xâ‚‚ - xâ‚
    â„“Ê¸ = yâ‚‚ - yâ‚

    # Fractional increment to closest segment point
    Ïµ = ((x - xâ‚) * â„“Ë£ + (y - yâ‚) * â„“Ê¸) / (â„“Ë£^2 + â„“Ê¸^2)
    Ïµ = clamp(Ïµ, 0, 1)

    # Closest segment point
    xâ€² = xâ‚ + Ïµ * â„“Ë£
    yâ€² = yâ‚ + Ïµ * â„“Ê¸

    return distance(Point(x, y), Point(xâ€², yâ€²))
end

"""
    distance(point::Point, line::Line)
Return the distance of a `point` to a `line`,
ie the shortest distance from the `point` to a point on the `line`.
If ``ð­`` is a unit vector parallel to the line and ``Î”ð±``
any vector connecting the `point` with any point on the line,
then the shortest distance between the line is
```math
|ð­ x Î”ð±| = |Î”ð±| |sinÎ¸|
```
where ``Î¸`` is the angle formed by vector ``Î”ð±`` and the line.
"""
function distance(point::Point, line::Line)
    x, y = point.x, point.y
    xâ‚, yâ‚ = line.pâ‚.x, line.pâ‚.y
    xâ‚‚, yâ‚‚ = line.pâ‚‚.x, line.pâ‚‚.y
    num = abs((xâ‚‚ - xâ‚) * (yâ‚ - y) - (yâ‚‚ - yâ‚) * (xâ‚ - x))
    den = sqrt((xâ‚‚ - xâ‚)^2 + (yâ‚‚ - yâ‚)^2)
    return num / den
end

#####
##### Bathymetry
#####

struct NeverworldBathymetry{G, B, P} <: Function
    grid :: G
    coastline_spline :: B
    scotia_ridge :: P
    rim_width :: Float64
    slope_width :: Float64
    shelf_width :: Float64
    shelf_depth :: Float64
    abyssal_depth :: Float64
    southern_channel_boundary :: Float64
    northern_channel_boundary :: Float64
    scotia_ridge_height :: Float64
    scotia_ridge_radius :: Float64
    scotia_ridge_width :: Float64
    scotia_ridge_center_longitude :: Float64
    scotia_ridge_center_latitude :: Float64
end

function NeverworldBathymetry(grid;
                              abyssal_depth = 4000,
                              shelf_depth = 200,
                              shelf_width = 2.5,
                              rim_width = shelf_width / 8,
                              slope_width = shelf_width,
                              southern_channel_boundary = -60,
                              northern_channel_boundary = -40,
                              scotia_ridge_height = 2000,
                              scotia_ridge_radius = 10,
                              scotia_ridge_width = 2,
                              scotia_ridge_center_longitude = 0,
                              scotia_ridge_center_latitude = (southern_channel_boundary +
                                                              northern_channel_boundary) / 2)

    # Use grid spacing for "beach width"
    Î” = max(grid.Î”Î»á¶ áµƒáµƒ, grid.Î”Ï†áµƒá¶ áµƒ)

    # Construct a cubic spline of the form `basin_depth(r)`, representing
    # the "basin component" of the Neverworld bathymetry where `r` is the distance
    # to the edge of the Neverworld (with units of degrees).
    r_coast = Î”
    r_beach = Î” + rim_width
    r_mid_shelf = Î” + rim_width + shelf_width / 2
    r_shelf = Î” + rim_width + shelf_width
    r_abyss = Î” + rim_width + shelf_width + slope_width

    Nx, Ny, Nz = size(grid)
    Î»_max = Î»node(Nx+1, 1, 1, grid, f, c, c) - Î»node(1, 1, 1, grid, f, c, c)
    Ï†_max = Ï†node(1, Ny+1, 1, grid, c, f, c) - Ï†node(1, 1, 1, grid, c, f, c)
    r_max = max(Î»_max, Ï†_max)

    basin_rim_distances = [0, r_coast,     r_beach,  r_mid_shelf,     r_shelf,       r_abyss,         r_max]
    basin_depths        = [0, 0,       shelf_depth,  shelf_depth, shelf_depth, abyssal_depth, abyssal_depth]
    coastline_spline = CubicSplineFunction{:x}(basin_rim_distances, basin_depths)

    R = scotia_ridge_radius 
    w = scotia_ridge_width
    H = abyssal_depth
    h = H - scotia_ridge_height

    # The so-called "clipped cone"
    scotia_ridge(r) = max(h, H * min(1, abs(r - R) / w))

    return NeverworldBathymetry(grid,
                                coastline_spline,
                                scotia_ridge,
                                Float64(rim_width),
                                Float64(slope_width),
                                Float64(shelf_width),
                                Float64(shelf_depth),
                                Float64(abyssal_depth),
                                Float64(southern_channel_boundary),
                                Float64(northern_channel_boundary),
                                Float64(scotia_ridge_height),
                                Float64(scotia_ridge_radius),
                                Float64(scotia_ridge_width),
                                Float64(scotia_ridge_center_longitude),
                                Float64(scotia_ridge_center_latitude))
end

function (nb::NeverworldBathymetry)(Î», Ï†)
    grid = nb.grid
    Nx, Ny, Nz = size(grid)

    # Four corners of the Neverworld
    Î»w = Î»node(1,       1, 1, grid, f, c, c)
    Î»e = Î»node(Nx+1,    1, 1, grid, f, c, c)
    Ï†s = Ï†node(1,       1, 1, grid, c, f, c)
    Ï†n = Ï†node(1,    Ny+1, 1, grid, c, f, c)

    # Draw lines along the six coasts of the Neverworld
    northern_vertices = (Point(Î»w, nb.northern_channel_boundary),
                         Point(Î»w, Ï†n),
                         Point(Î»e, Ï†n),
                         Point(Î»e, nb.northern_channel_boundary))

    southern_vertices = (Point(Î»e, nb.southern_channel_boundary),
                         Point(Î»e, Ï†s),
                         Point(Î»w, Ï†s),
                         Point(Î»w, nb.southern_channel_boundary))

    coastlines = [LineSegment(northern_vertices[1], northern_vertices[2]),
                  LineSegment(northern_vertices[2], northern_vertices[3]),
                  LineSegment(northern_vertices[3], northern_vertices[4]),
                  LineSegment(southern_vertices[1], southern_vertices[2]),
                  LineSegment(southern_vertices[2], southern_vertices[3]),
                  LineSegment(southern_vertices[3], southern_vertices[4])]

    # Minimum distance to the six rims of the Neverworld
    p = Point(Î», Ï†)
    r = minimum(distance(p, coastline) for coastline in coastlines)

    bottom_height = - nb.coastline_spline(r)

    # Scotia ridge
    Î»â‚› = nb.scotia_ridge_center_longitude
    Ï†â‚› = nb.scotia_ridge_center_latitude
    râ‚› = sqrt((Î» - Î»â‚›)^2 + (Ï† - Ï†â‚›)^2)
    ridge_height = - nb.scotia_ridge(râ‚›)

    # Limit to shallower depth
    bottom_height = max(ridge_height, bottom_height)

    return bottom_height
end

#####
##### Default vertical grid
#####

default_z = stretched_vertical_faces(surface_layer_Î”z = 8,
                                     surface_layer_height = 128,
                                     stretching = PowerLawStretching(1.02),
                                     depth = 4000)

#####
##### Boundary conditions
#####

# Default Neverworld wind stress profile
latitudes      = [-80,   -45,   -15,      0,    15,    45,  80]
zonal_stresses = [ +0,  -0.2,  +0.1,  +0.02,  +0.1,  -0.1,  +0] .* 1e-3 # kinematic wind stress
default_zonal_wind_stress = CubicSplineFunction{:y}(latitudes, zonal_stresses)

@inline cosine_target_buoyancy_distribution(Ï†, t, p) = p.Î”b * cos(Ï€ * Ï† / p.Î”Ï†)

@inline function seasonal_cosine_target_buoyancy_distribution(Ï†, t, p)
    Ï‰ = 2Ï€ / 360days
    Ïµ = p.Ïµ # amplitude of seasonal cycle

    # t=0: heart of Southern ocean summer
    return p.Î”b * (cos(Ï€ * Ï† / p.Î”Ï†) - Ïµ * cos(Ï‰ * t) * sin(Ï€/2 * Ï† / p.Î”Ï†))
end

@inline function buoyancy_relaxation(i, j, grid, clock, fields, parameters)
    k = grid.Nz

    # Target buoyancy distribution
    Ï† = Ï†node(i, j, k, grid, c, c, c)
    t = clock.time
    bâ˜… = parameters.bâ˜…(Ï†, t, parameters)

    Î”z = Î”zá¶œá¶œá¶œ(i, j, k, grid)
    tâ˜… = parameters.tâ˜…
    qâ˜… = Î”z / tâ˜…

    return @inbounds qâ˜… * (fields.b[i, j, k] - bâ˜…)
end

const c = Center()

function barotropic_substeps(Î”t, grid, gravitational_acceleration; cfl = 0.7)
    wave_speed = sqrt(gravitational_acceleration * grid.Lz)
    min_Î”x = minimum_xspacing(grid, c, c, c)
    min_Î”y = minimum_yspacing(grid, c, c, c)
    Î” = 1 / sqrt(1 / min_Î”x^2 + 1 / min_Î”y^2)
    minimum_substeps = ceil(Int, 2Î”t / (cfl * Î” / wave_speed))

    # Limit arbitrarily by 10
    return max(minimum_substeps, 10)
end

struct Default end

horizontal_resolution_tuple(n::Number) = (n, n)
horizontal_resolution_tuple(t::Tuple{Number, Number}) = t
horizontal_resolution_tuple(anything_else) =
    throw(ArgumentError("$anything_else is not a valid horizontal_resolution!"))

using Oceananigans.BuoyancyModels: g_Earth

function neverworld_simulation(arch;
                               ImmersedBoundaryType = PartialCellBottom,
                               horizontal_resolution = 1/4, # degrees
                               latitude = (-70, 0),
                               longitude = (0, 60),
                               z = default_z,
                               grid = nothing,
                               gravitational_acceleration = g_Earth,
                               momentum_advection = Default(),
                               tracer_advection = Default(),
                               closure = CATKEVerticalDiffusivity(),
                               tracers = (:b, :e),
                               buoyancy = BuoyancyTracer(),
                               buoyancy_relaxation_time_scale = 30days,
                               target_buoyancy_distribution = seasonal_cosine_target_buoyancy_distribution,
                               bottom_drag_coefficient = 2e-3,
                               equator_pole_buoyancy_difference = 0.06,
                               seasonal_cycle_relative_amplitude = 0.8,
                               surface_buoyancy_gradient = 1e-4, # sâ»Â¹
                               stratification_scale_height = 1000, # meters
                               time_step = 5minutes,
                               stop_time = 30days,
                               free_surface = nothing,
                               zonal_wind_stress = default_zonal_wind_stress)

    if isnothing(grid)
        # Build horizontal size
        Î”Î», Î”Ï† = horizontal_resolution_tuple(horizontal_resolution)
        LÎ» = longitude[2] - longitude[1]
        LÏ† = latitude[2] - latitude[1]
        NÎ» = ceil(Int, LÎ» / Î”Î»)
        NÏ† = ceil(Int, LÏ† / Î”Ï†)

        size = (NÎ», NÏ†, length(z)-1)
        halo = (5, 5, 5)
        topology = (Periodic, Bounded, Bounded)

        underlying_grid = LatitudeLongitudeGrid(arch; size, latitude, longitude, z, halo, topology)
        bathymetry = NeverworldBathymetry(underlying_grid)
        grid = ImmersedBoundaryGrid(underlying_grid, ImmersedBoundaryType(bathymetry))
    end

    if momentum_advection isa Default
        momentum_advection = VectorInvariant(vorticity_scheme  = WENO(grid.underlying_grid),
                                             divergence_scheme = WENO(grid.underlying_grid),
                                             vertical_scheme   = WENO(grid.underlying_grid))
    end

    if tracer_advection isa Default
        # Turn off advection of tke for efficiency
        tracer_advection = Dict()
        tracer_advection = Dict{Symbol, Any}(name => WENO(grid.underlying_grid) for name in tracers)
        tracer_advection[:e] = nothing
        tracer_advection = NamedTuple(name => tracer_advection[name] for name in tracers)
    end

    if isnothing(free_surface)
        substeps = barotropic_substeps(time_step, grid, gravitational_acceleration)
        free_surface = SplitExplicitFreeSurface(; gravitational_acceleration, substeps)
    end

    NÂ² = surface_buoyancy_gradient
    h  = stratification_scale_height 
    Î”b = equator_pole_buoyancy_difference 
    tâ˜… = buoyancy_relaxation_time_scale 
    Î”Ï† = abs(latitude[1])
    bâ˜… = target_buoyancy_distribution 
    Î¼  = bottom_drag_coefficient
    Ïµ  = seasonal_cycle_relative_amplitude 

    # Buoyancy flux
    parameters = (; Î”Ï†, Î”b, tâ˜…, bâ˜…, Ïµ)
    b_top_bc = FluxBoundaryCondition(buoyancy_relaxation, discrete_form=true; parameters)

    # Wind stress
    zonal_wind_stress_field = Field{Face, Center, Nothing}(grid)
    set!(zonal_wind_stress_field, zonal_wind_stress) 
    u_top_bc = FluxBoundaryCondition(interior(zonal_wind_stress_field, :, :, 1))

    # Bottom drag
    drag_u = FluxBoundaryCondition(u_immersed_bottom_drag, discrete_form=true, parameters=Î¼)
    drag_v = FluxBoundaryCondition(v_immersed_bottom_drag, discrete_form=true, parameters=Î¼)

    u_immersed_bc = ImmersedBoundaryCondition(bottom=drag_u) 
    v_immersed_bc = ImmersedBoundaryCondition(bottom=drag_v) 

    u_bottom_drag_bc = FluxBoundaryCondition(u_bottom_drag, discrete_form=true, parameters=Î¼)
    v_bottom_drag_bc = FluxBoundaryCondition(v_bottom_drag, discrete_form=true, parameters=Î¼)

    u_bcs = FieldBoundaryConditions(bottom=u_bottom_drag_bc, immersed=u_immersed_bc, top=u_top_bc)
    v_bcs = FieldBoundaryConditions(bottom=v_bottom_drag_bc, immersed=v_immersed_bc)
    b_bcs = FieldBoundaryConditions(top=b_top_bc)

    coriolis = HydrostaticSphericalCoriolis(scheme = ActiveCellEnstrophyConserving())

    model = HydrostaticFreeSurfaceModel(; grid, tracers, buoyancy, coriolis, free_surface,
                                        momentum_advection, tracer_advection, closure,
                                        boundary_conditions = (b=b_bcs, u=u_bcs, v=v_bcs))

    báµ¢(x, y, z) = Î”b + NÂ² * h * (exp(z / h) - 1)
    set!(model, b=báµ¢, e=1e-6)

    simulation = Simulation(model; Î”t=time_step, stop_time)

    return simulation
end
